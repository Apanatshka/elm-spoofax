module Elm

imports

Lexical
Common
Type
Def

context-free start-symbols

Start

context-free syntax

Start = Module

Module.Module = [module [UpperIds][Exports] where
[{Import "\n"}*]

[TopLevelStatements]]

Module.Main = [[{Import "\n"}*]

[TopLevelStatements]]

Exports.ExportAll = []
Exports.Exports = [([{Export ", "}*])]

Export.ADT     = [[UPPERID]([{ADTExport ", "}*])]
Export.ADTNone = [[UPPERID]]
Export.ADTAll  = [[UPPERID](..)]
Export         = Ref

ADTExport = ID
ADTExport.All = [..]

Import.Import = [import [UpperIds][ModAlias][Exposing]]

ModAlias.ModAlias = [ as [UPPERID]]
ModAlias.NoModAlias = []

Exposing.NoExposing = []
Exposing.Exposing = [ exposing ([{Export ", "}*])]

TopLevelStatement.AliasDef = [type alias [UPPERID] [ID*] = [Type]]
TopLevelStatement.ADTDef = [type [UPPERID] [ID*] = [{ADTDef " | "}+]]
TopLevelStatement = Def
TopLevelStatement.Port = [port [TypeDef]
port [ValueDef]]

TopLevelStatements = TopLevelStatement
TopLevelStatements.TLStatementSeq = TopLevelStatementNEWLINE TopLevelStatements {layout("1.first.col == 2.first.col")}

ADTDef.ADTCons = [[UPPERID] [Type+]]

syntax

TopLevelStatementNEWLINE-CF = TopLevelStatement-CF LAYOUT?-CF {enforce-newline}

TopLevelStatement-CF.Fixity =
  "infix" Fixity-CF LAYOUT?-CF INT-CF LAYOUT?-CF BinOp-CF

context-free syntax

Fixity.Left = [l]
Fixity.Right = [r]

template options

tokenize: "()"
keyword -/- [A-Za-z0-9\_\']

